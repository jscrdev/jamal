== Standard Built-In Parameter Parsing

Even though built-in macros are free to parse their input any way they want, there are support classes that ease this task.
The advantages of using these classes are twofold.

. These classes are fully tested, documented and easy to use saving development time and effort.

. The macros are more coherent and easier to understand for the users when they follow similar patterns.

[NOTE]
====
This document describes the format and use of the parameters of the built-in macros using these supporting classes.
====

The parameter parsing can scan the macro content from the start

* to the end,

* to a specific closing character, like new-line, or

* to the end of the macro content.

For example the built-in macro `include` has the follwoing two parameters among others:

* `verbatim`, and
* `lines`.

Using these an `include` macro can be written as:

[source]
----
{@include [verbatim lines=1..13] otherContent.txt}
----

In this case the parameters are enclosed between the `[` and `]` characters.
The core built-in macros use these separator characters.
Currently 8 built-in core macros have parameters.

Built-in macros in other packages are encouraged to use the `(` and `)` characters to enclose their parameters.
When the macro does not need content other than the parameters the recommended way is to use the whole content for the parameters without any separator characters.
Macros working on several lines, like `trimLines` usually use the first line of the content for parameters.

=== Introduction

Some macros need configuration.
For example, the `trimLines` macro in the snippet package can trim vertically if we instruct it.
Similarly, the left trimming can go all to the left, but it may also leave some margin if we specify some margin.
For this reason, this macro uses the two parameters:

* `trimVertical`, and

* `margin`.

This macro reserves the first line of the input and trims and returns only the rest of the lines.
The first line can contain parameters.

Other macros parse the whole input for parameters.
For example, the `snip:collect` macro collects snippets from files, and it does not convert anything from its input.
This macro parses the whole input looking for parameters.
Some macros start parsing for parameters if the first non-whitespace character after the macro id is `(` and the parsing stops at the matching `)`.

The class `Params` provides the tools to ease parameter parsing.

=== Parameter Sources

When macros needs parameters using the support classes, they look at different sources for the parameters.
First, they look at the macro's input and try to find the parameters there.
The parameters are specified in the format

[source,text]
----
name1=value1 name2=value2 ... nameN=valueN
----

The names are the parameter names that the macro may use.

The macro first passes the input to the parsing method.
When a parameter is not defined, the algorithm tries to look up a user-defined macro with the name as the parameter.
If you set `{@define margin=2}` somewhere in your Jamal source, this value will be used by every `trimLine` following it.
The parameter of the same name or a macro defined locally overrides the outer definition.
If the value is not defined as a parameter and is not defined as a user-defined macro, then the built-in macros can use default values.

NOTE: The macro `default` usually steps into the place of an undefined macro.
When you use an undefined user-defined macro, and Jamal does not find the macro, then it uses the macro named `default` assuming it is defined.
In this case, this macro is not used.
It is also an error if the macro used as the configuration has parameters unless the option `{@options lenient}` is specified.

The parameters have types.

* In the case of boolean parameters, the values `false`, `0`, or `no` are treated as false.
Everything else will result true.
If a required boolean parameter is not present as a parameter, then the option of the same name is taken into account.
The option is the one that you can set using the macro `{@options ...}`.
For example, you can specify `{@options trimVertical}` if you want all uses of the macro `trimLine` to trim vertically.
User-defined macros for a boolean parameter are NOT taken into account.
All boolean options are `false` by default; hence there are no mandatory boolean parameters.
If a boolean parameter is not defined in any way, then it will be `false`.

* Parameters can be string types, and integer types.
These can be defined in a parameter or as the value of a macro, as described above.

* Some parameters can have more than one value.
In that case the `name` of the parameter can appear more than once with multiple values assigned to it.

Macros may decide if they define any default value for a parameter or not.
In that case, the macro will error if the parameter is not defined, and there is also no user-defined macro for the parameter.

The parameter names can have aliases.
These can only be used inside the macro as parameters.
Alias names are not looked for as user-defined macros.
In other words the "main" name of the parameter can also be defined as macro, the aliases not.

The core macro uses `$forsep` as a parameter, and it has an alias `separator`.
If neither of them is used in the macro, only the user-defined macro `$forsep` is used.
If there is a `{@define separator=;}` in the code, it will not alter the execution of the `for` macro.
In the macro, the names and the aliases are interchangeable.

Macros can define parameters using only aliases.
In that case there is no way to globally define a value for the parameter as a macro.

Aliases are specific to the macro.
Different macros may use the same parameter name for other purposes providing different, or the same aliases.
It is up to the macro implementation what parameter names and what aliases they use.
However, the parameter names and the aliases are unique in the realm of a single macro implementation.

=== Details

In the parameter definition

[source,text]
----
name1=value1 name2=value2 ... nameN=valueN
----

the `value1, value2, ..., valueN` values are either _numeric_, _boolean_ or _string_ values.
The type depends on what the macro awaits for the specific parameter.

The values can be represented enclosed between `"`, `"""` or without any delimiter.
The `"..."` is a typical string representation.
Using the `"` is optional if there is no space or a particular escape sequence in the value.
For example `margin=1` is just as good as `margin="1"`.
On the other hand, `name=Peter Verhas` is not valid.
It has to be represented as `name="Peter Verhas"` because it contains a space.

A string starting and ending with a `"` character must not contain a newline character.
If you need multi-line strings, you can use the `\n` characters or use a multi-line string.
A multi-line string starts and ends with `"""`, three quote characters.

Some macros use only the first line for parameters.
Even in this case, the newline character inside a multi-line string is part of the value and does not stop the parsing.
The parsing stops only at the first newline character, which is not part of any value and is not escaped.

If there are many parameters, and the first line becomes too long, then the `\` character escapes the new line.
Subsequent lines can also escape the newline character using the `\` character.
It makes the parser skip the new line and go on parsing on the following line.

For example

[source,text]
----
{@someMacro header="""
This is the header
text and it is
multi-line
""" paging=true skip="A" skip="DD" skip="3.145" \
comment="this is still a parameter because of the \\ at the end of the previous line"
this is the input the macro will use for its result; everything else until here are parameters
}
----

When the macro asks the parser object to parse the input, it also specifies the parameter names it can handle.
If there is any parameter, which the macro does not handle, an error will occur.

The macro also specifies the type of the parameter.
If Jamal cannot convert the parameter value to the required type, an error will occur.

* Numeric parameters are integer. They can be enclosed between `"` or `"""`.

* Boolean parameters are false if they are present as a parameter, and the string value is either `false`, `0`, or `no`.
They are also false if they are not present as a parameter, and the name (not an alias) is also false as an option.
A name is `false` as an option if the macro `{@options name}` was not invoked in the current or higher scope, or if the option was invoked in the form `{@options ~name}`.
A boolean parameter can be set as `true` simply mentioning the name without `=` and any value.
It is possible to specify value, which will set the parameter to be true unless the value is `false`, `0`, or `no`.
It is recommended to use only the name, without any value assigned to it.

As you can see in the example, some parameters can have multiple values.
If there are multiple parameters defined, but the macro needs only one, an error will occur.
If there is only one value defined, but the macro needs a list, it will get a one-element list.
You cannot specify multiple values for such parameters using user-defined macros.
Jamal will not add the global or locally defined user macro to the parameter list if the parameter is specified at least once on the input.
If there are parameters, then those, and only values are used; the macro values are ignored.

Single line and multi-line strings have similar syntax as Java strings.
You can use the same escape sequences.

=== Examples

In this chapter, we list some examples of the syntax and the use of the parameters.
These examples come from the unit test file ./jamal-test/src/test/java/javax0/jamal/test/tools/params/TestParams.java.
The display of each example contains a definition line, an _INPUT_ part, and a _RESULT_ part.
The "INPUT" part shows the code that defines the values of the parameters.
It may also contain at the start some `options` or `define` macro in case some parameters get value from this source.
The "RESULT" part shows the calculated value of every parameter.

The definition line contains the parameters' names comma separated.
Each parameter has at least one name.
In case the parameter has an alias that can be used as a parameter name instead of the original name, it is given after a `|` character.
The parameter type is either `I` integer, `S` string, `B` boolean, or `L` list.
The last part following the last `:` is the default value, if there is any.

The test parses this definition string and calls the appropriate `orElse()`, `orElseInt()`, `asString()` and so on methods, which define the type of the parameter.

The RESULT part shows the parameters with the values as `key=value`.
When the returned value is a string, the result is enclosed between quotes.
When the value has some other type, it is shown like a casting operation `(type)` in front of the value.










==== Simple Parameters

This example shows the simple use of two integer, and a string parameter use.


[source,text]
----
margin:I,top:I,left:S
----
INPUT

[source,text]
----

margin=2 top=3 left="aligned"
----

RESULT

[source,text]
----

margin=2
top=3
left="aligned"

----


The integer parameters are not enclosed between `"` characters, although it is perfectly okay to do so. On the
other hand the value `"aligned"` is specified between quotes. This value is also eligible to be specified without
`"` as it contains neither space, not special escape character or the parsing closing character, which was `\n`
in this case.






==== Simple Boolean Example

Boolean parameters can be specified by the sheer presence. When a boolean parameter is not present and not
defined as an option, then the value is `false`.


[source,text]
----
left:B,right:B
----
INPUT

[source,text]
----

left
----

RESULT

[source,text]
----

left=(boolean)true
right=(boolean)false

----


Boolean `true` parameters can be represented by the appearance of the parameter on the line. In this example the
parameter`left` simple appears on the input without any value. The parameter `right` does not and it is also not
set to `true` as an option, so the value if false.






==== Parameter Defined as User Defined Macro


[source,text]
----
margin:I,top:I,left:S
----
INPUT

[source,text]
----

{@define margin=2}
top=3 left="aligned"
----

RESULT

[source,text]
----

margin=2
top=3
left="aligned"

----


In this example two values are present as parameters, but the parameter
`margin` is present by a user defined macro.






==== Value defined in User-defined Macro is Overridden by parameter

This example shows that a parameter defined in a user-defined macro is overridden by the definition of the
parameter on the input.


[source,text]
----
margin:I
----
INPUT

[source,text]
----

{@define margin=3}
margin=2
----

RESULT

[source,text]
----

margin=2

----


The parameter `margin` is defined as a user defined parameter, but the value `3` is ignored because it is also
defined on the input to be `2` and this is stronger.





==== Missing Parameter

When a parameter is used by a macro and there is no default value
for the parameter then not defining the parameter will be an error.


[source,text]
----
margin:I,missing:S
----
INPUT

[source,text]
----

margin=2
----

RESULT

[source,text]
----

javax0.jamal.api.BadSyntax: The key 'missing' for the macro 'test environment' is mandatory

----


The sample macro configuration requires two parameters: `margin` and `missing`.
None of them has default value and they are also no boolean or list values.
Margin is defined in the input but the parameter `missing`, aptly named, is indeed missing.
This makes the parameter parsing to throw an exception.






==== Continuation line

This example shows that the first line can be extended using continuation lines, which are escaped using `\`
character at the end of the line.


[source,text]
----
margin:I,top:I,left:S
----
INPUT

[source,text]
----

margin=2 top=3 \
      left="aligned"
----

RESULT

[source,text]
----

margin=2
top=3
left="aligned"

----

The parameters `margin` and `top` are defined on the first line.
The parameter `left` would have been too long.
It got into the next line.
To do that the last character on the previous line is a `\` character.





==== Multi-line String parameter, one line

This example shows how you can use multi-line strings as parameters.
Multi-line strings start and end with the `"""` characters and can span multiple lines.
In this example the sample multi-line string does not span multiple line showing that this is not a must.
The use also demonstrates that single `"` characters do not need to be escaped, but they may be escaped.


[source,text]
----
left:S
----
INPUT

[source,text]
----

left="""ali"gn\"ed"""
----

RESULT

[source,text]
----

left="ali\"gn\"ed"

----


The value of the parameter`left` is specified as a multi-line string, and it contains two `"` characters, one escaped, the other without escaping.





==== Multi-line String parameter, two lines

This example shows how you can use multi-line strings as parameters.
Multi-line strings start and end with the `"""` characters and can span multiple lines.
In this example the sample multi-line string spans two lines.


[source,text]
----
left:S
----
INPUT

[source,text]
----

left="""alig
ned"""
----

RESULT

[source,text]
----

left="alig\nned"

----


This time the parameter `aligned` contains a new line in the string.





==== Multi-valued parameter can have single value

Multi-valued parameters can apper more than once as parameter.
But it is not a must.
They may be missing, or specified only one time.
This example shows that a multi-valued parameter can appear one time.


[source,text]
----
left:L
----
INPUT

[source,text]
----

left="aligned"
----

RESULT

[source,text]
----

left=[aligned]

----


The parameter `left` is a `L` list as it is declared by the testing macro.
Even though it is a list it appears only once as a parameter.
The result for the macro is that this parameter will be a list that has a single element.






==== Multi-valued Parameter with Multiple Values

This example shows how to specify multiple values for a parameter that is declared to have multiple values.


[source,text]
----
left:L
----
INPUT

[source,text]
----

left="aligned"left="alignad"
----

RESULT

[source,text]
----

left=[aligned,alignad]

----







==== Boolean Parameters

This example shows an extensive list of all the possibilities how a boolean parameter can be defined.


[source,text]
----
trueOption:B,explicitFalseOption:B,implicitFalseOption:B,falseAsNo:B,falseAsFalse:B,
falseAs0:B,trueAsTrue:B,trueAsYes:B,trueAs1:B,trueAsAnything:B,trueStandalone:B
----
INPUT

[source,text]
----

{@options trueOption|~explicitFalseOption}
falseAsNo=no falseAsFalse=false falseAs0=0 trueAsTrue=true \
trueAsYes=yes trueAs1=1 trueAsAnything="really anything goes" trueStandalone
----

RESULT

[source,text]
----

trueOption=(boolean)true
explicitFalseOption=(boolean)false
implicitFalseOption=(boolean)false
falseAsNo=(boolean)false
falseAsFalse=(boolean)false
falseAs0=(boolean)false
trueAsTrue=(boolean)true
trueAsYes=(boolean)true
trueAs1=(boolean)true
trueAsAnything=(boolean)true
trueStandalone=(boolean)true

----


The parameter `trueOption` is set globally calling the macro `options`. The `explicitFalseOption` is set to false
on the same line. This is an example about how to set and reset options, even more than one at the same time.

* The parameter `implicitFalseOption` is not set anywhere. It is required by the macro, it is notdefined as an
option and also not as a parameter. This parameter will be `false` by default.

* The parameter `falseAsNo` is set to `no` as a parameter. Similarly `falseAsFalse` is set to `false`, `falseAs0`
is set to `0`.

* As the false parameters are listed with all the values the `true` values are also listed with some of the
possible assignment values that result a `true` value. `trueAsTrue` is set to `true`. The parameter `trueAsYes`
is set to `yes`, `trueAs1` is set to `1`. Finally `trueAsAnything` is set to an arbitrary string that will be
converted to a true value.

* The parameter `trueStandalone` demonstrate the use of a boolean parameter when the name is simply listed as a
parameter without any value. In this case the presence of the parameter signals the true value it presents.

Using some arbitrary value to signal a boolean value is usually not the best choice. Other than choosing
presenting the value in the form of a standalone parameter, or with value `yes`, `true`, `no`, `0`, `false` is a
matter of taste. Use the one that you feel makes your code the most readable. Jamal source can get very easily
really messy and complex. Strive to make it as simple as possible.



